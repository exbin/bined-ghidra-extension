buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }    
    }
}

plugins {
    id 'java'
    id 'distribution'
}
import org.gradle.util.VersionNumber

version = '0.2.0'

sourceCompatibility = '17'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
    binedVersion = '0.2.1'
    binaryDataVersion = '0.2.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

// Disable tests on build
if (!gradle.startParameter.taskNames.any {it.endsWith("test")}) {
    tasks.withType(Test) {enabled = false}
}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier = "tests"
    archiveBaseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.java
}

javadoc {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.bined.ghidra.GhidraExtension'
}

task buildPack (type: Zip, dependsOn: tasks.jar) {
    archiveBaseName = "bined-ghidra-extension"
    
    into ('BinedExtension') {
        duplicatesStrategy = 'exclude'
        from (jar) {
            into 'lib'
            rename { filename -> "BinedExtension.jar" }
        }

        from (configurations.runtimeClasspath) {
            include 'bined*.jar'
            include 'binary_data*.jar'
            include 'jdom-legacy*.jar'
            include 'bcprov-jdk15on-1.70.jar'
            into 'lib'
        }

        from ('data') {
            into 'data'
        }
        
        from 'Module.manifest'
        from 'certification.manifest'
        from 'extension.properties'
        from 'LICENSE.txt'
    }
    from 'src/dist/readme.txt'
}
// publish.dependsOn buildPack

def binedLibrary = { String libName ->
    return (libName.endsWith('-SNAPSHOT')) ? ":${libName}-${binedVersion}" : "org.exbin.bined:${libName}:${binedVersion}"
}

def binaryDataLibrary = { String libName ->
    return (libName.endsWith('-SNAPSHOT')) ? ":${libName}-${binaryDataVersion}" : "org.exbin.auxiliary:${libName}:${binaryDataVersion}"
}

repositories {
    mavenCentral()
    flatDir {
        dirs "lib"
    }
}

def ghidraInstallDir

if (System.env.GHIDRA_INSTALL_DIR) {
    ghidraInstallDir = System.env.GHIDRA_INSTALL_DIR
} else if (project.hasProperty("GHIDRA_INSTALL_DIR")) {
    ghidraInstallDir = project.getProperty("GHIDRA_INSTALL_DIR")
}

if (ghidraInstallDir) {
    if (gradle.startParameter.taskNames.contains('buildExtension')) {
        apply from: new File(ghidraInstallDir).getCanonicalPath() + "/support/buildExtension.gradle"
    }
} else {
    throw new GradleException("GHIDRA_INSTALL_DIR is not defined!")
}

dependencies {
    compileOnly fileTree(dir: ghidraInstallDir + '/Ghidra/patch', include: "**/*.jar")
    compileOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Configurations', include: "**/*.jar")
    compileOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Features', include: "**/*.jar")
    compileOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Framework', include: "**/*.jar")
    compileOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Processors', include: "**/*.jar")
    compileOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Debug', include: "**/*.jar")
    compileOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Extensions',
						 include: "**/*.jar", exclude: project.name)
    implementation binedLibrary('bined-core')
    implementation binedLibrary('bined-extended')
    implementation binedLibrary('bined-highlight-swing')
    implementation binedLibrary('bined-operation')
    implementation binedLibrary('bined-operation-swing')
    implementation binedLibrary('bined-swing')
    implementation binedLibrary('bined-swing-extended')
    implementation binaryDataLibrary('binary_data')
    implementation binaryDataLibrary('binary_data-paged')
    implementation binaryDataLibrary('binary_data-delta')

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
    implementation "org.jdom:jdom-legacy:1.1.3"

    testImplementation group : 'junit', name: 'junit', version: '4.13.2'
}

jar {
    manifest {
        attributes(
            'Specification-Title': "BinedExtension",
            'Specification-Version': "10.4",
            'Main-Class': mainClass
        )
    }
}
